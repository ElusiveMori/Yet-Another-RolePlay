library Unit requires Core {
	//! textmacro CommandHeader takes CMD_NAME
		private static method $CMD_NAME$(CommandThread thread) {
	//! endtextmacro

	//! textmacro CommandStart takes CMD_NAME
			s_thread = thread;
			thread.selection.run(function thistype.$CMD_NAME$_aux);
		}

		private static method $CMD_NAME$_aux() {
			unit what = GetEnumUnit();
			UnitData unitData;

			if (HasPlayerUnitAccess(s_thread.owner, what)) {
				unitData = UnitData[what];
	//! endtextmacro

	//! textmacro CommandEnd
			}
		}
	//! endtextmacro

	struct Unit[] {
		private static CommandThread s_thread;
		private static real s_real1;
		private static real s_real2;
		private static integer s_int;
		private static integer s_r;
		private static integer s_g;
		private static integer s_b;
		private static integer s_a;
		private static boolean s_bool;
		private static string s_string;
		private static playercolor s_color;

		private static method Command_UnitRemove_aux() {
			unit what = GetEnumUnit();

			if (HasPlayerUnitAccess(s_thread.owner, what)) {
				RemoveUnit(what);
			}
		}

		private static method Command_UnitRemove(CommandThread thread) {
			string command = thread.args.next();
			boolean valid  = true;
			integer target = -1;
			s_thread = thread;

			if (StringLength(command) == 0) {
			} else if (command == "me") {
				thread.selection.clear();
				GroupEnumUnitsOfPlayer(thread.selection.getGroup(), P(thread.owner), null);
			} else {
				target = PlayerIdFromString(command);

				if (target >= 0) {
					thread.selection.clear();
					GroupEnumUnitsOfPlayer(thread.selection.getGroup(), P(target), null);
				} else {
					valid = false;
				}
			}

			thread.selection.run(function thistype.Command_UnitRemove_aux);
		}

		private static method Command_UnitGive_aux() {
			unit what = GetEnumUnit();
			UnitData unitData = 0;

			if (HasPlayerUnitAccess(s_thread.owner, what) && unitData.realOwner == s_thread.owner) {
				unitData = UnitData[what];

				if (s_bool) {
					unitData.realOwner = s_int;
				}

				unitData.currentOwner = s_int;
			}
		}

		private static method Command_UnitGive(CommandThread thread) {
			string targetName = thread.args.restSimple();
			s_int = -1;
			s_thread = thread;

			if (targetName == "me") {
				s_int = thread.owner;
			} else {
				s_int = PlayerIdFromString(targetName);
			}

			if (s_int < 0) return;
			s_bool = (s_int < 12);

			thread.selection.run(function thistype.Command_UnitGive_aux);
		}

		private static method Command_UnitGiveForce_aux() {
			unit what = GetEnumUnit();
			UnitData unitData;

			if (HasPlayerUnitAccess(s_thread.owner, what)) {
				unitData = UnitData[what];

				if (s_bool) {
					unitData.realOwner = s_int;
				}

				unitData.currentOwner = s_int;
			}
		}

		private static method Command_UnitGiveForce(CommandThread thread) {
			string targetName = thread.args.restSimple();
			s_int = -1;
			s_thread = thread;

			if (targetName == "me") {
				s_int = thread.owner;
			} else {
				s_int = PlayerIdFromString(targetName);
			}

			if (s_int < 0) return;
			s_bool = (s_int < 12);

			thread.selection.run(function thistype.Command_UnitGiveForce_aux);
		}

		private static method Command_UnitStep_aux() {
			unit what = GetEnumUnit();
			CommandThread thread;

			thread = CommandThread.create(s_thread.owner);
			thread.selection.addUnit(what);
			thread.pushCommands("repeat " + I2S(s_int) + " " + s_string);
			thread.start.execute();

			s_int = s_int + 1;
		}

		private static method Command_UnitStep(CommandThread thread) {
			s_string = thread.args.restParsed();
			s_int = 0;
			s_thread = thread;

			thread.selection.run(function thistype.Command_UnitStep_aux);
		}

		private static method Command_UnitAbility_aux_add() {
			unit what = GetEnumUnit();

			if (HasPlayerUnitAccess(s_thread.owner, what)) {
				UnitAddAbility(what, s_int);
			}
		}

		private static method Command_UnitAbility_aux_rem() {
			unit what = GetEnumUnit();

			if (HasPlayerUnitAccess(s_thread.owner, what)) {
				UnitRemoveAbility(what, s_int);
			}
		}

		private static method Command_UnitAbility(CommandThread thread) {
			string str = thread.args.next();
			s_int = String2Id(thread.args.next());
			s_thread = thread;

			if (str == "add") {
				thread.selection.run(function thistype.Command_UnitAbility_aux_add);
			} else if (str == "remove") {
				thread.selection.run(function thistype.Command_UnitAbility_aux_rem);
			}
		}

		private static method Command_UnitGlobal(CommandThread thread) {
			string targetName = thread.args.next();
			string command = thread.args.restParsed();
			CommandThread newThread;
			integer target;

			if (targetName == "me") {
				target = thread.owner;
			} else {
				target = PlayerIdFromString(targetName);
			}

			if (target < 0) return;

			thread = CommandThread.create(thread.owner);
			GroupEnumUnitsOfPlayer(thread.selection.getGroup(), P(target), null);
			thread.pushCommands(command);
			thread.start.execute();
		}

		//! runtextmacro CommandHeader("Command_UnitKill")
		//! runtextmacro CommandStart("Command_UnitKill")
			KillUnit(what);
		//! runtextmacro CommandEnd()

		//! runtextmacro CommandHeader("Command_UnitExplode")
		//! runtextmacro CommandStart("Command_UnitExplode")
			SetUnitExploded(what, true);
			KillUnit(what);
		//! runtextmacro CommandEnd()

		//! runtextmacro CommandHeader("Command_UnitSize")
			s_real1 = S2R(thread.args.next()) / 100;
		//! runtextmacro CommandStart("Command_UnitSize")
			unitData.size = s_real1;
		//! runtextmacro CommandEnd()

		//! runtextmacro CommandHeader("Command_UnitSizeDelta")
			s_real1 = S2R(thread.args.next()) / 100;
		//! runtextmacro CommandStart("Command_UnitSizeDelta")
			unitData.size = unitData.size + s_real1;
		//! runtextmacro CommandEnd()

		//! runtextmacro CommandHeader("Command_UnitAngle")
			s_real1 = S2R(thread.args.next());
		//! runtextmacro CommandStart("Command_UnitAngle")
			unitData.angle = s_real1;
		//! runtextmacro CommandEnd()

		//! runtextmacro CommandHeader("Command_UnitAngleDelta")
			s_real1 = S2R(thread.args.next());
		//! runtextmacro CommandStart("Command_UnitAngleDelta")
			unitData.angle = unitData.angle + s_real1;
		//! runtextmacro CommandEnd()

		//! runtextmacro CommandHeader("Command_UnitAngleRandom")
		//! runtextmacro CommandStart("Command_UnitAngleRandom")
			unitData.angle = GetRandomReal(0, 360);
		//! runtextmacro CommandEnd()

		//! runtextmacro CommandHeader("Command_UnitMoveX")
			s_real1 = S2R(thread.args.next());
		//! runtextmacro CommandStart("Command_UnitMoveX")
			unitData.x = unitData.x + s_real1;
		//! runtextmacro CommandEnd()

		//! runtextmacro CommandHeader("Command_UnitMoveY")
			s_real1 = S2R(thread.args.next());
		//! runtextmacro CommandStart("Command_UnitMoveY")
			unitData.y = unitData.y + s_real1;
		//! runtextmacro CommandEnd()

		//! runtextmacro CommandHeader("Command_UnitMoveXY")
			s_real1 = S2R(thread.args.next());
			s_real2 = S2R(thread.args.next());
		//! runtextmacro CommandStart("Command_UnitMoveXY")
			unitData.x = unitData.x + s_real1;
			unitData.y = unitData.y + s_real2;
		//! runtextmacro CommandEnd()

		//! runtextmacro CommandHeader("Command_UnitSetX")
			s_real1 = S2R(thread.args.next());
		//! runtextmacro CommandStart("Command_UnitSetX")
			unitData.x = s_real1;
		//! runtextmacro CommandEnd()

		//! runtextmacro CommandHeader("Command_UnitSetY")
			s_real1 = S2R(thread.args.next());
		//! runtextmacro CommandStart("Command_UnitSetY")
			unitData.y = s_real1;
		//! runtextmacro CommandEnd()

		//! runtextmacro CommandHeader("Command_UnitSetXY")
			s_real1 = S2R(thread.args.next());
			s_real2 = S2R(thread.args.next());
		//! runtextmacro CommandStart("Command_UnitSetXY")
			unitData.x = s_real1;
			unitData.y = s_real2;
		//! runtextmacro CommandEnd()

		//! runtextmacro CommandHeader("Command_UnitMoveDirection")
			real angle = Deg2Rad(S2R(thread.args.next()));
			real amount = S2R(thread.args.next());
			s_real1 = Cos(angle) * amount;
			s_real2 = Sin(angle) * amount;
		//! runtextmacro CommandStart("Command_UnitMoveDirection")
			unitData.x = unitData.x + s_real1;
			unitData.y = unitData.y + s_real2;
		//! runtextmacro CommandEnd()

		//! runtextmacro CommandHeader("Command_UnitTint")
			s_r = R2I(S2R(thread.args.next()) * 255 / 100);
			s_g = R2I(S2R(thread.args.next()) * 255 / 100);
			s_b = R2I(S2R(thread.args.next()) * 255 / 100);
			s_a = 255 - R2I(S2R(thread.args.next()) * 255 / 100);
		//! runtextmacro CommandStart("Command_UnitTint")
			unitData.setVertexColor(s_r, s_g, s_b, s_a);
		//! runtextmacro CommandEnd()

		//! runtextmacro CommandHeader("Command_UnitColor")
			integer id = PlayerIdFromString(thread.args.restParsed());

			if (id >= 0)
				s_color = ConvertPlayerColor(id);
			else
				s_color = ConvertPlayerColor(15);
		//! runtextmacro CommandStart("Command_UnitColor")
			unitData.color = s_color;
		//! runtextmacro CommandEnd()

		//! runtextmacro CommandHeader("Command_UnitAnimSpeed")
			s_real1 = S2R(thread.args.restParsed()) / 100;
		//! runtextmacro CommandStart("Command_UnitAnimSpeed")
			unitData.animSpeed = s_real1;
		//! runtextmacro CommandEnd()

		//! runtextmacro CommandHeader("Command_UnitSpeed")
			s_real1 = S2R(thread.args.restParsed());
		//! runtextmacro CommandStart("Command_UnitSpeed")
			unitData.speed = s_real1;
		//! runtextmacro CommandEnd()

		//! runtextmacro CommandHeader("Command_UnitRemoveBuffs")
		//! runtextmacro CommandStart("Command_UnitRemoveBuffs")
			UnitRemoveBuffs(what, true, true);
		//! runtextmacro CommandEnd()

		//! runtextmacro CommandHeader("Command_UnitRemoveCooldowns")
		//! runtextmacro CommandStart("Command_UnitRemoveCooldowns")
			UnitResetCooldown(what);
		//! runtextmacro CommandEnd()

		//! runtextmacro CommandHeader("Command_UnitPlayAnim")
			s_string = thread.args.next();
			s_int = S2I(thread.args.next()) - 1;

			if (s_int < 0)
				s_int = 0;
		//! runtextmacro CommandStart("Command_UnitPlayAnim")
			unitData.setAnim(s_string);
			unitData.queueAnimCount(s_string, s_int);
		//! runtextmacro CommandEnd()

		//! runtextmacro CommandHeader("Command_UnitQueueAnim")
			s_string = thread.args.next();
			s_int = S2I(thread.args.next()) - 1;

			if (s_int < 0)
				s_int = 0;
		//! runtextmacro CommandStart("Command_UnitQueueAnim")
			unitData.queueAnimCount(s_string, s_int);
		//! runtextmacro CommandEnd()

		//! runtextmacro CommandHeader("Command_Tag")
			s_string = thread.args.next();
			s_bool = (thread.args.next() == "off");
		//! runtextmacro CommandStart("Command_Tag")
			AddUnitAnimationProperties(what, s_string, !s_bool);
		//! runtextmacro CommandEnd()

		//! runtextmacro CommandHeader("Command_UnitFly")
			s_real1 = S2R(thread.args.next());
		//! runtextmacro CommandStart("Command_UnitFly")
			unitData.z = s_real1;
		//! runtextmacro CommandEnd()

		//! runtextmacro CommandHeader("Command_UnitFlyDelta")
			s_real1 = S2R(thread.args.next());
		//! runtextmacro CommandStart("Command_UnitFlyDelta")
			unitData.z = unitData.z + s_real1;
		//! runtextmacro CommandEnd()

		//! runtextmacro CommandHeader("Command_UnitPath")
			string arg = thread.args.next();

			if (arg == "off") {
				s_bool = false;
			} else if (arg == "on") {
				s_bool = true;
			} else {
				return;
			}
		//! runtextmacro CommandStart("Command_UnitPath")
			SetUnitPathing(what, s_bool);
		//! runtextmacro CommandEnd()

		//! runtextmacro CommandHeader("Command_UnitId")
		//! runtextmacro CommandStart("Command_UnitId")
			MessageTo(s_thread.owner, "ID: " + Id2String(unitData.id) + " (" + I2S(unitData.id) + ")");
		//! runtextmacro CommandEnd()

		//! runtextmacro CommandHeader("Command_UnitLife")
			s_real1 = S2R(thread.args.next());
		//! runtextmacro CommandStart("Command_UnitLife")
			unitData.life = s_real1;
		//! runtextmacro CommandEnd()

		//! runtextmacro CommandHeader("Command_UnitMana")
			s_real1 = S2R(thread.args.next());
		//! runtextmacro CommandStart("Command_UnitMana")
			unitData.mana = s_real1;
		//! runtextmacro CommandEnd()

		private static method onInit() {
			Command.register("remove",  thistype.Command_UnitRemove);
			Command.register("give",    thistype.Command_UnitGive);
			Command.register("givef",   thistype.Command_UnitGiveForce);
			Command.register("kill",    thistype.Command_UnitKill);
			Command.register("explode", thistype.Command_UnitExplode);
			Command.register("size",    thistype.Command_UnitSize);
			Command.register("sized",   thistype.Command_UnitSizeDelta);
			Command.register("fa",      thistype.Command_UnitAngle);
			Command.register("fd",      thistype.Command_UnitAngleDelta);
			Command.register("fr",      thistype.Command_UnitAngleRandom);
			Command.register("movex",   thistype.Command_UnitMoveX);
			Command.register("movey",   thistype.Command_UnitMoveY);
			Command.register("move",    thistype.Command_UnitMoveXY);
			Command.register("posx",    thistype.Command_UnitSetX);
			Command.register("posy",    thistype.Command_UnitSetY);
			Command.register("pos",     thistype.Command_UnitSetXY);
			Command.register("movea",   thistype.Command_UnitMoveDirection);
			Command.register("tint",    thistype.Command_UnitTint);
			Command.register("color",   thistype.Command_UnitColor);
			Command.register("aspeed",  thistype.Command_UnitAnimSpeed);
			Command.register("speed",   thistype.Command_UnitSpeed);
			Command.register("buff",    thistype.Command_UnitRemoveBuffs);
			Command.register("cd",      thistype.Command_UnitRemoveCooldowns);
			Command.register("play",    thistype.Command_UnitPlayAnim);
			Command.register("tag",     thistype.Command_Tag);
			Command.register("playq",   thistype.Command_UnitQueueAnim);
			Command.register("fly",     thistype.Command_UnitFly);
			Command.register("flyd",    thistype.Command_UnitFlyDelta);
			Command.register("path",    thistype.Command_UnitPath);
			Command.register("step",    thistype.Command_UnitStep);
			Command.register("id",      thistype.Command_UnitId);
			Command.register("life",    thistype.Command_UnitLife);
			Command.register("mana",    thistype.Command_UnitMana);
			Command.register("ability", thistype.Command_UnitAbility);
			Command.register("global",  thistype.Command_UnitGlobal);

			Alias.saveall("gl", "global");
		}
	}
}