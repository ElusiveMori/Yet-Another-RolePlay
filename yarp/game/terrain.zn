library Terrain requires Core {
	constant integer TERRAIN_SAMPLE  = 'pA0c';
	constant integer TERRAIN_CIRCLE1 = 'pA0d';
	constant integer TERRAIN_CIRCLE2 = 'pA0e';
	constant integer TERRAIN_CIRCLE3 = 'pA0f';
	constant integer TERRAIN_CIRCLE4 = 'pA0g';
	constant integer TERRAIN_SQUARE1 = 'pA0h';
	constant integer TERRAIN_SQUARE2 = 'pA0i';
	constant integer TERRAIN_SQUARE3 = 'pA0j';
	constant integer TERRAIN_SQUARE4 = 'pA0k';

	constant integer HEIGHT_RAISEPOINT    = 'pA0m';
	constant integer HEIGHT_LOWERPOINT    = 'pA0n';
	constant integer HEIGHT_RAISECIRCLE1  = 'pA0o';
	constant integer HEIGHT_LOWERCIRCLE1  = 'pA0p';
	constant integer HEIGHT_RAISECIRCLE2  = 'pA0q';
	constant integer HEIGHT_LOWERCIRCLE2  = 'pA0r';
	constant integer HEIGHT_RAISECIRCLE3  = 'pA0s';
	constant integer HEIGHT_LOWERCIRCLE3  = 'pA0t';
	constant integer HEIGHT_REMOVEDEFORMS = 'pA0u';

	constant integer DOODAD_PLACE  = 'pA0w';
	constant integer DOODAD_REMOVE = 'pA0x';
	constant integer DOODAD_KILL   = 'pA0y';
	constant integer DOODAD_SAMPLE = 'pA0z';

	constant integer BUILD_DOODAD = 'pA0v';

	struct Terrain[] {
		static integer sampled[];
		static integer sampledDoods[];
		static real doodSizes[];
		static real heights[];

		static integer count;
		static integer removeWho;
		static real removeX;
		static real removeY;
		static real removeDx;
		static real removeDy;
		static real removeDist;
		static DeformationList removeCur;

		static integer sampleWho;

		static Table tileWhitelist;

		private static method removeDeformsRadius() {
			count = 0;

			while (removeCur > 0 && count < 256) {
				if (removeCur.owner == removeWho) {
					removeDx = (removeCur.x - removeX);
					removeDy = (removeCur.y - removeY);
					removeDist = (removeDx * removeDx) + (removeDy * removeDy);

					if (removeDist < 65536) {
						removeCur.destroy();
					}
				}
	

				removeCur = removeCur.next;
				count = count + 1;
			}

			if (removeCur > 0) {
				removeDeformsRadius.execute();
			}
		}

		private static method removeDeformsPlayer() {
			count = 0;

			while (removeCur > 0 && count < 512) {
				if (removeCur.owner == removeWho) {
					removeCur.destroy();
				}

				removeCur = removeCur.next;
				count = count + 1;
			}

			if (removeCur > 0) {
				removeDeformsPlayer.execute();
			}
		}

		private static method aux_removeDood() {
			RemoveDestructable(GetEnumDestructable());
		}

		private static method aux_killDood() {
			KillDestructable(GetEnumDestructable());
		}

		private static method aux_sampleDood() {
			sampledDoods[sampleWho] = GetDestructableTypeId(GetEnumDestructable());
		}

		private static method onCast() {
			integer spellId = GetSpellAbilityId();
			unit caster = GetTriggerUnit();
			integer owner = PID(GetOwningPlayer(caster));
			real x, y;
			rect area;
			integer variation;

			if (Spawner.isSpawner(caster)) {
				x = GetSpellTargetX();
				y = GetSpellTargetY();

				if (spellId == TERRAIN_SAMPLE) {
					sampled[owner] = GetTerrainType(x, y);
				}

				if (sampled[owner] > 0) {
					if (spellId == TERRAIN_CIRCLE1) {
						SetTerrainType(x, y, sampled[owner], -1, 1, 0);
					} else if (spellId == TERRAIN_CIRCLE2) {
						SetTerrainType(x, y, sampled[owner], -1, 2, 0);
					} else if (spellId == TERRAIN_CIRCLE3) {
						SetTerrainType(x, y, sampled[owner], -1, 3, 0);
					} else if (spellId == TERRAIN_CIRCLE4) {
						SetTerrainType(x, y, sampled[owner], -1, 4, 0);
					} else if (spellId == TERRAIN_SQUARE1) {
						SetTerrainType(x, y, sampled[owner], -1, 1, 1);
					} else if (spellId == TERRAIN_SQUARE2) {
						SetTerrainType(x, y, sampled[owner], -1, 2, 1);
					} else if (spellId == TERRAIN_SQUARE3) {
						SetTerrainType(x, y, sampled[owner], -1, 3, 1);
					} else if (spellId == TERRAIN_SQUARE4) {
						SetTerrainType(x, y, sampled[owner], -1, 4, 1);
					}
				}

				if (spellId == HEIGHT_RAISEPOINT) {
					DeformationList.create(owner, x, y, heights[owner], 4);
				} else if (spellId == HEIGHT_RAISECIRCLE1) {
					DeformationList.create(owner, x, y, heights[owner], 256);
				} else if (spellId == HEIGHT_RAISECIRCLE2) {
					DeformationList.create(owner, x, y, heights[owner], 512);
				} else if (spellId == HEIGHT_RAISECIRCLE3) {
					DeformationList.create(owner, x, y, heights[owner], 768);
				} else if (spellId == HEIGHT_LOWERPOINT) {
					DeformationList.create(owner, x, y, -heights[owner], 4);
				} else if (spellId == HEIGHT_LOWERCIRCLE1) {
					DeformationList.create(owner, x, y, -heights[owner], 256);
				} else if (spellId == HEIGHT_LOWERCIRCLE2) {
					DeformationList.create(owner, x, y, -heights[owner], 512);
				} else if (spellId == HEIGHT_LOWERCIRCLE3) {
					DeformationList.create(owner, x, y, -heights[owner], 768);
				} else if (spellId == HEIGHT_REMOVEDEFORMS) {
					removeWho = owner;
					removeX = x;
					removeY = y;
					removeCur = DeformationList.top;

					removeDeformsRadius.execute();
				} else if (spellId == DOODAD_PLACE) {
					if (sampledDoods[owner] > 0) {
						if (Listing.doodVariations.has(sampledDoods[owner])) {
							variation = GetRandomInt(0, Listing.doodVariations[sampledDoods[owner]] - 1);
						} else {
							variation = GetRandomInt(0, 2);
						}
						CreateDestructable(sampledDoods[owner], x, y, GetRandomReal(0, 360), doodSizes[owner], variation);
					}
				} else if (spellId == DOODAD_REMOVE) {
					area = Rect(0, 0, 512, 512);
					MoveRectTo(area, x, y);
					EnumDestructablesInRect(area, null, function thistype.aux_removeDood);
					RemoveRect(area);
					area = null;
				} else if (spellId == DOODAD_KILL) {
					area = Rect(0, 0, 512, 512);
					MoveRectTo(area, x, y);
					EnumDestructablesInRect(area, null, function thistype.aux_killDood);
					RemoveRect(area);
					area = null;
				} else if (spellId == DOODAD_SAMPLE) {
					area = Rect(0, 0, 512, 512);
					MoveRectTo(area, x, y);
					sampleWho = owner;
					EnumDestructablesInRect(area, null, function thistype.aux_sampleDood);
					RemoveRect(area);
					area = null;
				}
			}
		}

		private static method Command_Height(CommandThread thread) {
			heights[thread.owner] = S2R(thread.args.next());
		}

		private static method Command_RemoveDeforms(CommandThread thread) {
			string who = thread.args.next();
			integer target;

			if (SL(who) == 0) {
				target = thread.owner;
			} else {
				target = PlayerIdFromString(who);

				if (target == -1)
					return;
			}

			if ((target != thread.owner) && (GetPlayerAccessLevel(target) >= GetPlayerAccessLevel(thread.owner))) {
				return;
			}

			removeWho = target;
			removeCur = DeformationList.top;

			removeDeformsPlayer.execute();
		}

		private static method Command_Terrain(CommandThread thread) {
			integer id = String2Id(thread.args.next());

			if (Listing.tiles.boolean.has(id)) {
				sampled[thread.owner] = id;
			} else {
				MessageTo(thread.owner, "Invalid tile ID.");
			}
		}

		private static method Command_Dood(CommandThread thread) {
			string command = thread.args.next();

			if (command == "size") {
				doodSizes[thread.owner] = S2R(thread.args.next()) / 100;
			} else {
				if (Sub(command, 0, 1) == "_") {
					sampledDoods[thread.owner] = String2Id(Sub(command, 1, 5));
				}
			}
		}

		private static method onInit() {
			integer i;
			trigger castTrigger = CreateTrigger();
			TriggerAddAction(castTrigger, function thistype.onCast);
			TriggerRegisterAnyUnitEventBJ(castTrigger, EVENT_PLAYER_UNIT_SPELL_CAST);
			castTrigger = null;

			Command.register("height",        thistype.Command_Height,        CMD_DIRECT);
			Command.register("removedeforms", thistype.Command_RemoveDeforms, CMD_DIRECT);
			Command.register("terrain",       thistype.Command_Terrain,       CMD_DIRECT);
			Command.register("dood",          thistype.Command_Dood,          CMD_DIRECT);

			for (0 <= i < 12) {
				doodSizes[i] = 1;
			}
		}
	}
}